import string

io.readline : IO String
io.readline = __readline

io.print : String -> IO Unit
io.print = fun s : String, __print s

io.pure : forall A, A -> IO A
io.pure = fun A, fun x : A, __pure A x

io.bind : forall A, forall B, (A -> IO B) -> IO A -> IO B
io.bind = fun A, fun B, fun func : A -> IO B, fun a : IO A, __bind A B func a
// TODO do we want __bind to not take A and B?

io.map : forall A, forall B, (A -> B) -> IO A -> IO B
io.map = fun A, fun B, fun f : (A -> B), io.bind A B (io.pure B . f)

// Reads lines from input until an empty line is read
io.readlines : IO [String]
io.readlines =
  io.bind String [String]
  (fun l : String, if empty Char l then io.pure [String] [:String]
    else io.map [String] [String] (fun lines : [String], l :: lines) io.readlines)
  io.readline

io.repl : (String -> String) -> IO Unit
io.repl =
  fun f : String -> String,
  io.bind String Unit 
  (fun input : String, 
    if empty Char input then io.pure Unit () else
    io.bind Unit Unit 
    (fun _ : Unit, io.repl f)
    <| io.print ((f input) <> "\n")
  )
  io.readline
